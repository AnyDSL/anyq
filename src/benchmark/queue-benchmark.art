
fn @upsweep_exponential(body: fn(i32) -> ()) {
	fn @(?min & ?max & ?exp) loop(min: i32, max: i32, exp: i32) -> () {
		if min <= max {
			@body(min);
			loop(min * exp, max, exp);
		}
	}
	loop
}


struct QueueBenchmarkStatistics {
	num_enqueues: i32,
	num_enqueue_attempts: i32,
	num_dequeues: i32,
	num_dequeue_attempts: i32
}

fn init_benchmark_stats() = QueueBenchmarkStatistics {
	num_enqueues = 0,
	num_enqueue_attempts = 0,
	num_dequeues = 0,
	num_dequeue_attempts = 0
};

struct QueueBenchmark {
	reset: fn() -> (),
	run: fn(i32) -> (),
	read_stats: fn() -> QueueBenchmarkStatistics
}


fn @run_queue_benchmark(num_threads_min: i32, num_threads_max: i32, benchmark: QueueBenchmark) -> i32 {
	let instrumentation = create_instrumentation();

	print_string("platform;device_name\n");
	instrumentation.print_device_info();
	print_char('\n');
	print_char('\n');

	print_string("num_threads;t/ms;num_enqueues;num_enqueue_attempts;num_dequeues;num_dequeue_attempts\n");

	let N = 10;

	for num_threads in upsweep_exponential(num_threads_min, num_threads_max, 2) {
		for _ in range(0, N) {
			print_i32(num_threads); print_char(';');

			benchmark.reset();

			for instrumentation.time() {
				benchmark.run(num_threads);
			}

			let stats = benchmark.read_stats();

			print_char(';');
			print_i32(stats.num_enqueues); print_char(';');
			print_i32(stats.num_enqueue_attempts); print_char(';');
			print_i32(stats.num_dequeues); print_char(';');
			print_i32(stats.num_dequeue_attempts); print_char('\n');
			print_flush();
		}
	}

	0
}
