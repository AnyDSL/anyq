

fn producer_consumer_queue_concurrent_push_pop_benchmark(block_size: i32, attempts_per_thread: i32, p_enq: i32, p_deq: i32, workload_size: i32, queue_size: i32) {
	let device = createAccDevice();

	let queue = createConcurrentProducerConsumerIndexQueue(device, queue_size);


	for grid in device.launch_1d(div_up(queue_size, block_size), block_size) {
		queue.reset(grid);
	}

	@|num_threads: i32| {
		for grid in device.launch_1d(div_up(num_threads, block_size), block_size) {
			for wave in grid.waves() {
				for thread in wave.threads() {
					let thread_id = wave.idx() * wave.num_threads() + thread.idx(0);

					let mut rand_state = rng::xorseed32(thread_id);

					let sample = @|p: i32| -> bool {
						if p < 100 {
							rand_state = rng::xorshift32(rand_state);
							rand_state < (((-1 as u64) * p as u64) / 100) as u32
						}
						else {
							true
						}
					};

					let simulate_workload = @|| {
						for _ in range(0, workload_size) {
							rand_state = rng::xorshift32(rand_state);
						}
					};

					for _ in range(0, attempts_per_thread) {
						if sample(p_enq) {
							for _ in queue.push(thread) {
								simulate_workload();
								rand_state
							};
						}

						wave.barrier();

						if sample(p_deq) {
							for _, _ in queue.pop(thread) {
								simulate_workload();
							};
						}

						wave.barrier();
					}
				}
			}
		}
	}
}
